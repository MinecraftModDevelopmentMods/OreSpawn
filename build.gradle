def corePlugin = ''

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'gradle'
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.11'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

javadoc {
    failOnError = false
}

description = 'OreSpawn'
def mod_file = getModFile()
def mc_version = "1.11.2"
def short_version = getVersion("VERSION", mod_file)
version = mc_version + "-" + short_version
if (System.getenv().BUILD_NUMBER) {
    version += '.' + System.getenv().BUILD_NUMBER
}
group = "com.mcmoddev"
archivesBaseName = "OreSpawn"
sourceCompatibility = targetCompatibility = "1.8"

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ''
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = '1.11.2-13.20.1.2386'
    runDir = 'run'
    mappings = 'stable_32'
//    coreMod = ''
    makeObfSourceJar = false

	replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

repositories {
    maven { // Mantle, TCon, JEI
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { // CCL, CCC, NEI
        name 'Chickenbones Repo'
        url 'http://chickenbones.net/maven/'
    }
    maven { // The One Probe
        name 'tterrag'
        url 'http://maven.tterrag.com/'
    }
    maven {
        name 'MMD'
        url 'https://maven.mcmoddev.com/'
    }
    maven { // MCMultipart
        name 'amadornes'
        url 'http://maven.amadornes.com/'
    }
    maven { // Tesla
        name 'epoxide'
        url 'http://maven.epoxide.org'
    }
    maven { // CraftTweaker, ZenScript
        name 'jared maven'
        url 'http://blamejared.com/maven'
    }
    maven { // IC2
        name 'industrialcraft'
        url 'http://maven.ic2.player.to'
    }
    maven { // WAILA/HWYLA
    	name 'tehnut'
    	url 'http://tehnut.info/maven'
    }
    maven { // CoFH
    	name 'Covers Maven'
    	url 'http://maven.covers1624.net'
    }
	maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
	maven {
        name 'opencomputers'
        url 'http://maven.cil.li/'
    }
    maven { // Mekanism, TAIGA
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

dependencies {
    // None
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': short_version, 'mcversion': project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
//      attributes 'FMLCorePluginContainsFMLMod': 'true'
//      attributes 'FMLCorePlugin': corePlugin
//      attributes 'FMLAT' : ''
    }
}

task apiJar(type: Jar, dependsOn: classes) {
    classifier = 'api'
    from sourceSets.main.allSource
    exclude('com/mcmoddev/orespawn/impl/**')
    exclude('com/mcmoddev/orespawn/impl/features/**')
    exclude('com/mcmoddev/orespawn/json/**')
    exclude('com/mcmoddev/orespawn/world/**')
    exclude('com/mcmoddev/orespawn/commands/**')
    exclude('com/mcmoddev/orespawn/data/**')
    exclude('com/mcmoddev/orespawn/*.java')
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task deobfJar(type: Jar) {
    classifier = 'deobf'
    from sourceSets.main.output
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing. 
    // Using project.propName would cause the script to fail validation if 
    // the property did not exist. 
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives apiJar
    archives devJar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact apiJar {
                classifier 'api'
            }
            artifact devJar {
                classifier 'dev'
            }
            artifact deobfJar {
                classifier 'deobf'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = "245586"
        changelog = file("CHANGELOG.txt")
        releaseType = "release"
        def projName = "OreSpawn"
        def displayVersion = getVersion("VERSION", mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += '.' + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$project.description $displayVersion"
//          relations {
//          }
        }
        addArtifact(apiJar) {
            displayName = "$project.description $displayVersion API"
        }
        addArtifact(sourceJar) {
            displayName = "$project.description $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$project.description $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$project.description $displayVersion Javadoc"
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', secret.sonarHost
        property 'sonar.organization', secret.sonarOrganization
        property 'sonar.login', secret.sonarToken
        property 'sonar.projectName', project.archivesBaseName
        property 'sonar.projectKey', "$project.group:$project.archivesBaseName"
    }
}

String getModFile() {
    String path = 'src/main/java/com/mcmoddev/orespawn/data/Constants.java'
    return path
}

String getVersion(String type, String mod_file) {
    String major = '0'
    String revision = '0'
    String patch = '0'
    String prefix = "public static final String $type = \""
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim()
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2)
            String[] pts = s.split("\\.")

            major = pts[0]
            revision = pts[1]
            patch = pts[2]
        }
    }
    return "$major.$revision.$patch"
}
